/*
 * Build file for grabl
 * A Gradle plugin for ABL based on PCT written in Groovy
 *
 * Based on:
 *  - https://docs.gradle.org/current/userguide/plugins.html
 *  - https://docs.gradle.org/current/userguide/custom_plugins.html
 *  - https://github.com/gradle/gradle/tree/master/subprojects/docs/src/samples/customPlugin/plugin
 *  - https://github.com/gradle/gradle/tree/master/subprojects/docs/src/samples/javaGradlePlugin
 */

plugins {
    id 'com.gradle.build-scan' version '1.11'
    id 'me.champeau.buildscan-recipes' version '0.2.3'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.9.9'
    id 'nebula.release' version '6.1.0'
}

// Apply the groovy plugin to add support for Groovy
apply plugin: 'groovy'

group = 'io.gitlab.grabl'

ext {
    vcsBaseUrl = 'https://gitlab.com/grabl/grabl'
}

repositories {
    jcenter()
}

dependencies {
    // Use the awesome Spock testing and specification framework
    testCompile('org.spockframework:spock-core:1.1-groovy-2.4-rc-4') {
        exclude module: 'groovy-all'
    }
    testCompile 'junit:junit:4.12'
    // required by spock for mocking classes
    testCompile 'net.bytebuddy:byte-buddy:1.6.9'
}


// Configure development and publishing helper plugins
// Inspired by: https://github.com/gradle/kotlin-dsl/blob/master/plugins/build.gradle.kts

class GradlePluginMetaData {
    String name
    String displayName
    String id
    String implementationClass

    GradlePluginMetaData(String name, String displayName, String id, String implementationClass) {
        this.name = name
        this.displayName = displayName
        this.id = id
        this.implementationClass = implementationClass
    }
}

def pluginList = [
    new GradlePluginMetaData(
        project.name,
        'A Gradle plugin for ABL based on PCT',
        "${project.group}.${project.name}",
        "${project.group}.GrablPlugin"
    )
]

pluginBundle {
    website = 'https://gitlab.com/grabl/grabl'
    vcsUrl = 'https://gitlab.com/grabl/grabl'
    tags = ['abl', 'pct']
}

pluginList.each { plugin ->
    // Configuration for the `java-gradle-plugin` plugin
    // https://docs.gradle.org/4.3.1/userguide/javaGradle_plugin.html
    gradlePlugin {
        plugins {
            create(plugin.id) {
                id = plugin.id
                implementationClass = plugin.implementationClass
            }
        }
    }

    // Configuration for the `com.gradle.plugin-publish` plugin
    // https://plugins.gradle.org/docs/publish-plugin
    // https://guides.gradle.org/publishing-plugins-to-gradle-plugin-portal/
    pluginBundle {
        plugins {
            create(plugin.id) {
                id = plugin.id
                displayName = plugin.displayName
                description = plugin.displayName
            }
        }
    }
}

postRelease.dependsOn(publishPlugins)

// Configure nebula.release plugin
// https://github.com/nebula-plugins/nebula-release-plugin

/* The default versions for dev snapshots generated by nebula.release
 * contain commit counts, name of the source branch and a tag for
 * uncommitted changes, e.g.:
 *
 *   0.1.0-dev.1.dev.8.uncommitted+bug.9.publishing.runs.before.release.dfc09a7
 *
 * However the Gradle Plugin Portal restricts versions to a string that
 * 1) is more than 1 and less than 20 characters long;
 * 2) includes a number;
 * 3) that matches the regular expression: [a-zA-Z0-9\-\.\[\]\:\+]*
 *
 * Customise the strategy to only include information that is most
 * useful in determining the source that was used to produce the build.
 * That is: stage, GitLab issue ID, commit ID. This relies on
 * convention to name the branches starting with the GitLab issue ID.
 */

import nebula.plugin.release.NetflixOssStrategies
import org.ajoberstar.gradle.git.release.opinion.Strategies.PreRelease

def DEV_STRATEGY = NetflixOssStrategies.DEVELOPMENT.copyWith(
        preReleaseStrategy: PreRelease.STAGE_FLOAT
)

release {
    versionStrategy DEV_STRATEGY
    defaultVersionStrategy = DEV_STRATEGY
}

nebulaRelease {
    shortenedBranchPattern = /(?:(?:bug|chore|feature|refactor|release)(?:-|\/))?(\d+)/
}

// Configure build-scans
// https://docs.gradle.com/build-scan-plugin/

buildScan {
    licenseAgreementUrl = 'https://gradle.com/terms-of-service'
    licenseAgree = 'yes'

    if (System.getenv('CI')) {
        publishAlways()
        tag 'CI'
    } else {
        tag 'dev'
    }
}

buildScanRecipes {
    recipe('remote',
            url: 'https://github.com/gimoh/gradle-buildscan-recipes/raw/1be41ba9fd9c684aef0b8c1e9bae716ef85038af/src/recipes/gitlab-ci.groovy',
            cache: true
    )
    recipe('remote',
            url: 'https://github.com/gimoh/gradle-buildscan-recipes/raw/46bf70d41c6caf0a99b7583443723bc056fa46a8/src/recipes/grgit.groovy',
            cache: true,
            baseUrl: "$vcsBaseUrl/commit"
    )
}
